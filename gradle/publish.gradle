task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// maven artifact

apply plugin: 'maven-publish'

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

// publishing to maven central
// see https://github.com/rakutentech/android-buildconfig/tree/master/publish

def user =                  System.getenv('MAVEN_CENTRAL_USER')
def password =              System.getenv('MAVEN_CENTRAL_PASSWORD')
def signingKeyId =          System.getenv("MAVEN_CENTRAL_SIGNING_KEY_ID")
def signingPassword =       System.getenv("MAVEN_CENTRAL_SIGNING_PASSWORD")
def signingKeyRingFile =    System.getenv("MAVEN_CENTRAL_SIGNING_KEY_RING_FILE")

publishing {
    publications {
        "${project.name}"(MavenPublication) {
            groupId project.group
            artifactId project.name // explicitly name project so the property won't be shadowed
            version project.version
            from components.java
            artifact(javadocJar)
            artifact(sourcesJar)
        }
    }

    repositories {
        maven {
            it.name "MavenCentralStaging"
            credentials {
                it.username user
                it.password password
            }
            it.url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

ext["signing.keyId"] = signingKeyId
ext["signing.password"] = signingPassword
ext["signing.secretKeyRingFile"] = signingKeyRingFile

apply plugin: 'signing'

signing {
    sign publishing.publications
}

task ensureMavenCentralCredentials {
    doFirst {
        if (!user?.trim() || !password?.trim() || !signingKeyId?.trim() || !signingPassword.trim() || !signingKeyRingFile.trim()) {
            throw new GradleException("You must define environment variables for " +
                    "MAVEN_CENTRAL_USER, MAVEN_CENTRAL_PASSWORD, " +
                    "MAVEN_CENTRAL_SIGNING_KEY_ID, MAVEN_CENTRAL_SIGNING_PASSWORD, and MAVEN_CENTRAL_SIGNING_KEY_RING_FILE.")
        }
    }
}

publish.dependsOn ensureMavenCentralCredentials